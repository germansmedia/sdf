# DAY 10

Let's do BenesiPine1. It's also iterating, like the bulb, but more interesting.

This M3F comes with an explanation, so this helps decyphering the x86 assembly.

## Options

```
.Version = 2
.DEscale = 1
.SIpower = 2
.Double Scale = 2.0
.Double xOff = 2.0
```

# Description

```
// STEP1: "Benesi fold 1"
tx=(x*sqrt(2/3)-z*sqrt(1/3))*sqrt(1/2);
z=abs(x*sqrt(1/3) + z*sqrt(2/3));   	
x=abs(tx-y*sqrt(1/2));          
y=abs(tx+y*sqrt(1/2));

tx=x*sqrt(1/2)+y*sqrt(1/2);
y=-x*sqrt(1/2)+y*sqrt(1/2); 
x=tx*sqrt(2/3)+z*sqrt(1/3);
z=-tx*sqrt(1/3)+z*sqrt(2/3);	

x=scale*x-offset;  //scale =2    offset=2       
y=scale*y;
z=scale*z;

// STEP2: "Benesi pinetree"
xt = x*x; yt = y*y; zt = z*z;
t = 2*x/sqrt(yt+zt);
x = xt-yt-zt+Cx;
z = t*(yt-zt)+Cy;
y = 2*t*y*z+Cz;
```

## Constants

```
Double = 0.816496580927726  // sqrt(2/3)
Double = 0.5773502691896258  // sqrt(1/3)
Double = 0.7071067811865475  // sqrt(1/2)
```

## Disassembly

`[eax]` is a pointer to loop variable `x`
`[edx]` is a pointer to loop variable `y`
`[ecx]` is a pointer to loop variable `z`

So, looks like the calling convention of this is Delphi/FreePascal `register`, where the first three parameters are passed via `eax`, `edx` and `ecx`.

With the return address and the original `ebp` saved on the stack (2x u32 = 8 bytes), `[ebp + 0x8]` is the fourth parameter, containing a pointer to some descriptor block (could be `TIteration3D`) accesed via `[esi]`:

```
esi +
0x00:  ?
0x08:  ?
0x10:  ?
0x18:  Cx (TIteration3D.J1, pixel test X)
0x20:  Cz (TIteration3D.J2, pixel test Y)
0x28:  Cy (TIteration3D.J3, pixel test Z)
0x30:  pointer to constants and parameters (TIteration3D.PVar)
```

The pointer from `[esi + 0x30]` points to another block (TIteration3D.PVar), accessed via `[edi]`, but in both directions:

```
edi +
-0x18:  offset
-0x10:  scale
-0x08:  ? always 0.5
 0x00:  sqrt(2/3), STT
 0x08:  sqrt(1/3), SOT
 0x10:  sqrt(1/2), SOH
```

Temporary/local variables are accessed via `[ebp]`:

```
ebp +
0x00:
-0x08:  tx
-0x10:  ty
-0x18:  tz
-0x20:  t
```

```
0:  55                      push   ebp                             // save ebp
1:  8b ec                   mov    ebp,esp                         // ebp is base of stack frame

3:  81 ec 30 00 00 00       sub    esp,0x30                        // reserve 6 QWORDs on the stack for temporary/local variables, accessed via [ebp-...]

9:  53                      push   ebx                             // save ebx
a:  56                      push   esi                             // save esi
b:  57                      push   edi                             // save edi

c:  8b 75 08                mov    esi,DWORD PTR [ebp+0x8]         // esi = [ebp + 0x8]
f:  8b 7e 30                mov    edi,DWORD PTR [esi+0x30]        // edi = [esi + 0x30]
12: 8b d8                   mov    ebx,eax                         // ebx = eax

14: d9 d0                   fnop                                   // ..

// tx=(x*sqrt(2/3)-z*sqrt(1/3))*sqrt(1/2);

16: dd 03                   fld    QWORD PTR [ebx]                 // x;
18: dc 0f                   fmul   QWORD PTR [edi]                 // x SOT;
1a: dd 01                   fld    QWORD PTR [ecx]                 // z; x SOT;
1c: dc 4f 08                fmul   QWORD PTR [edi+0x8]             // z STT; x SOT;
1f: d9 e0                   fchs                                   // -z STT; x SOT;
21: de c1                   faddp  st(1),st                        // x SOT - z STT;
23: dc 4f 10                fmul   QWORD PTR [edi+0x10]            // SOH (x SOT - z STT);
26: dd 5d f8                fstp   QWORD PTR [ebp-0x8]             // tx = SOH (x SOT - z STT)

// z=abs(x*sqrt(1/3) + z*sqrt(2/3));   	

29: dd 03                   fld    QWORD PTR [ebx]                 // x;
2b: dc 4f 08                fmul   QWORD PTR [edi+0x8]             // x STT;
2e: dd 01                   fld    QWORD PTR [ecx]                 // z; x STT;
30: dc 0f                   fmul   QWORD PTR [edi]                 // z SOT; x STT;
32: de c1                   faddp  st(1),st                        // x STT + z SOT;
34: d9 e1                   fabs                                   // |x STT + z SOT|;

36: 90                      nop

// x=abs(tx-y*sqrt(1/2));          

37: dd 45 f8                fld    QWORD PTR [ebp-0x8]             // tx; |x STT + z SOT|;
3a: dd 02                   fld    QWORD PTR [edx]                 // y; tx; |x STT + z SOT|;
3c: dc 4f 10                fmul   QWORD PTR [edi+0x10]            // y SOH; tx; |x STT + z SOT|;
3f: d9 e0                   fchs                                   // -y SOH; tx; |x STT + z SOT|;
41: de c1                   faddp  st(1),st                        // tx - y SOH; |x STT + z SOT|;
43: d9 e1                   fabs                                   // |tx - y SOH|; |x STT + z SOT|;

45: 90                      nop

// y=abs(tx+y*sqrt(1/2));

46: dd 45 f8                fld    QWORD PTR [ebp-0x8]             // tx; |tx - y SOH|; |x STT + z SOT|;
49: dd 02                   fld    QWORD PTR [edx]                 // y; tx; |tx - y SOH|; |x STT + z SOT|;
4b: dc 4f 10                fmul   QWORD PTR [edi+0x10]            // y SOH; tx; |tx - y SOH|; |x STT + z SOT|;
4e: de c1                   faddp  st(1),st                        // tx + y SOH; |tx - y SOH|; |x STT + z SOT|;
50: d9 e1                   fabs                                   // |tx + y SOH|; |tx - y SOH|; |x STT + z SOT|;

52: 90                      nop

53: dd 1a                   fstp   QWORD PTR [edx]                 // y = |tx + y SOH|; |tx - y SOH|; |x STT + z SOT|;
55: dd 1b                   fstp   QWORD PTR [ebx]                 // x = |tx - y SOH|; |x STT + z SOT|;
57: dd 19                   fstp   QWORD PTR [ecx]                 // z = |x STT + z SOT|;

59: 90                      nop

// tx=x*sqrt(1/2)+y*sqrt(1/2);

5a: dd 03                   fld    QWORD PTR [ebx]                 // x;
5c: dc 02                   fadd   QWORD PTR [edx]                 // x + y;
5e: dc 4f 10                fmul   QWORD PTR [edi+0x10]            // (x + y) SOH;
61: dd 5d f8                fstp   QWORD PTR [ebp-0x8]             // tx = (x + y) SOH

64: 90                      nop

// y=-x*sqrt(1/2)+y*sqrt(1/2); 

65: dd 03                   fld    QWORD PTR [ebx]                 // x;
67: d9 e0                   fchs                                   // -x;
69: dc 02                   fadd   QWORD PTR [edx]                 // -x + y;
6b: dc 4f 10                fmul   QWORD PTR [edi+0x10]            // (-x + y) SOH;

6e: 90                      nop

// x=tx*sqrt(2/3)+z*sqrt(1/3);

6f: dd 45 f8                fld    QWORD PTR [ebp-0x8]             // tx; (-x + y) SOH;
72: dc 0f                   fmul   QWORD PTR [edi]                 // tx STT; (-x + y) SOH;
74: dd 01                   fld    QWORD PTR [ecx]                 // z; tx STT; (-x + y) SOH;
76: dc 4f 08                fmul   QWORD PTR [edi+0x8]             // z SOT; tx STT; (-x + y) SOH;
79: de c1                   faddp  st(1),st                        // tx STT + z SOT; (-x + y) SOH;

7b: 90                      nop

// z=-tx*sqrt(1/3)+z*sqrt(2/3);	

7c: dd 45 f8                fld    QWORD PTR [ebp-0x8]             // tx; tx STT + z SOT; (-x + y) SOH;
7f: dc 4f 08                fmul   QWORD PTR [edi+0x8]             // tx SOT; tx STT + z SOT; (-x + y) SOH;
82: d9 e0                   fchs                                   // -tx SOT; tx STT + z SOT; (-x + y) SOH;
84: dd 01                   fld    QWORD PTR [ecx]                 // z; -tx SOT; tx STT + z SOT; (-x + y) SOH;
86: dc 0f                   fmul   QWORD PTR [edi]                 // z STT; -tx SOT; tx STT + z SOT; (-x + y) SOH;
88: de c1                   faddp  st(1),st                        // -tx SOT + z STT; tx STT + z SOT; (-x + y) SOH;

8a: 90                      nop

8b: dd 19                   fstp   QWORD PTR [ecx]                 // z = -tx SOT + z STT; tx STT + z SOT; (-x + y) SOH;
8d: dd 1b                   fstp   QWORD PTR [ebx]                 // x = tx STT + z SOT; (-x + y) SOH;
8f: dd 1a                   fstp   QWORD PTR [edx]                 // y = (-x + y) SOH

91: 90                      nop

// x=scale*x-offset;

92: dd 03                   fld    QWORD PTR [ebx]                 // x;
94: dc 4f f0                fmul   QWORD PTR [edi-0x10]            // x scale;
97: dc 67 e8                fsub   QWORD PTR [edi-0x18]            // x scale - offset;
9a: dd 1b                   fstp   QWORD PTR [ebx]                 // x = x scale - offset

// y=scale*y;

9c: dd 02                   fld    QWORD PTR [edx]                 // y;
9e: dc 4f f0                fmul   QWORD PTR [edi-0x10]            // y scale;
a1: dd 1a                   fstp   QWORD PTR [edx]                 // y = y scale

// z=scale*z;

a3: dd 01                   fld    QWORD PTR [ecx]                 // z;
a5: dc 4f f0                fmul   QWORD PTR [edi-0x10]            // z scale;
a8: dd 19                   fstp   QWORD PTR [ecx]                 // z = z scale

aa: 90                      nop

// xt = x*x; yt = y*y; zt = z*z;

ab: dd 03                   fld    QWORD PTR [ebx]                 // x;
ad: d8 c8                   fmul   st,st(0)                        // x x;
af: dd 5d f8                fstp   QWORD PTR [ebp-0x8]             // xt = x x

b2: dd 02                   fld    QWORD PTR [edx]                 // y;
b4: d8 c8                   fmul   st,st(0)                        // y y;
b6: dd 5d f0                fstp   QWORD PTR [ebp-0x10]            // yt = y y

b9: dd 01                   fld    QWORD PTR [ecx]                 // z;
bb: d8 c8                   fmul   st,st(0)                        // z z;
bd: dd 5d e8                fstp   QWORD PTR [ebp-0x18]            // zt = z z

// t = 2*x/sqrt(yt+zt);

c0: dd 03                   fld    QWORD PTR [ebx]                 // x;
c2: d8 c0                   fadd   st,st(0)                        // 2 x;
c4: dd 45 f0                fld    QWORD PTR [ebp-0x10]            // yt; 2 x;
c7: dc 45 e8                fadd   QWORD PTR [ebp-0x18]            // yt + zt; 2 x;
ca: d9 fa                   fsqrt                                  // sqrt(yt + zt); 2 x;
cc: de f9                   fdivp  st(1),st                        // 2 x / sqrt(yt + zt);
ce: dd 5d e0                fstp   QWORD PTR [ebp-0x20]            // t = 2 x / sqrt(yt + zt)

d1: 90                      nop

// x = xt-yt-zt+Cx;
// z = t*(yt-zt)+Cy;
// y = 2*t*y*z+Cz;

d2: dd 45 f8                fld    QWORD PTR [ebp-0x8]             // xt;
d5: dc 65 f0                fsub   QWORD PTR [ebp-0x10]            // xt - yt;
d8: dc 65 e8                fsub   QWORD PTR [ebp-0x18]            // xt - yt - zt;

db: 90                      nop

dc: dd 45 e0                fld    QWORD PTR [ebp-0x20]            // t; xt - yt - zt;
df: dd 45 f0                fld    QWORD PTR [ebp-0x10]            // yt; t; xt - yt - zt;
e2: dc 65 e8                fsub   QWORD PTR [ebp-0x18]            // yt - zt; t; xt - yt - zt;
e5: de c9                   fmulp  st(1),st                        // t (yt - zt); xt - yt - zt;

e7: 90                      nop

e8: dd 45 e0                fld    QWORD PTR [ebp-0x20]            // t; t (yt - zt); xt - yt - zt;
eb: d8 c0                   fadd   st,st(0)                        // 2 t; t (yt - zt); xt - yt - zt;
ed: dc 09                   fmul   QWORD PTR [ecx]                 // 2 t z; t (yt - zt); xt - yt - zt;
ef: dc 0a                   fmul   QWORD PTR [edx]                 // 2 t z y; t (yt - zt); xt - yt - zt;

f1: 90                      nop

f2: dc 46 20                fadd   QWORD PTR [esi+0x20]            // 2 t z y + Cz; t (yt - zt); xt - yt - zt;
f5: dd 1a                   fstp   QWORD PTR [edx]                 // y = 2 t z y + Cz; t (yt - zt); xt - yt - zt;

f7: 90                      nop

f8: dc 46 28                fadd   QWORD PTR [esi+0x28]            // t (yt - zt) + Cy; xt - yt - zt;
fb: dd 19                   fstp   QWORD PTR [ecx]                 // z = t (yt - zt) + Cy; xt - yt - zt;

fd: 90                      nop

fe: dc 46 18                fadd   QWORD PTR [esi+0x18]            // xt - yt - zt + Cx;
101:    dd 1b                   fstp   QWORD PTR [ebx]             // x = xt - yt - zt + Cx

103:    d9 d0                   fnop

105:    8b c3                   mov    eax,ebx                     // restore eax
107:    5f                      pop    edi                         // restore edi
108:    5e                      pop    esi                         // restore esi
109:    5b                      pop    ebx                         // restore ebx

10a:    89 ec                   mov    esp,ebp                     // restore stack frame
10c:    5d                      pop    ebp                         // restore ebp

10d:    c2 08 00                ret    0x8                         // return, and pop the 4th parameter as well
```

## Thoughts

Maybe `x x + y y + z z` need to stay within a certain range, much like is the case with the Bulb. That's worth trying out.

Did not work exactly yet. But we learned a lot thusfar.
