# DAY 9

- found a MandelBulb that exists as SDF
- implemented diffuse lighting
- implemented shadow to simple light source
- fog, soft shadows, phong lighting model (because that's the one I remember)

## Good Reading

http://blog.hvidtfeldts.net/index.php/2011/06/distance-estimated-3d-fractals-part-i/

- might get glow out of playing with the closest distance, just like shadow

- reflections (but on another fractal)

- various distance estimators
- kaleidoscopic IFS
- other distance estimators
- Jacobian distance estimator
- scalar distance estimator
- escape length gradient
- potential gradient
- mandelbox
- dual numbers

## Amazing Box

Also, on the MandelBulb3D fromt, here is an example disassembly of the Amazing Box from MandelBulb3D. Let's decompile:

### x86 code

```
0:  55                      push   ebp                        // establish stack frame
1:  8b ec                   mov    ebp,esp
3:  56                      push   esi                        // save esi
4:  53                      push   ebx                        // save ebx
5:  8b 75 08                mov    esi,DWORD PTR [ebp+0x8]    // esi = ebp[8]
8:  89 c3                   mov    ebx,eax                    // ebx = eax
a:  8b 76 30                mov    esi,DWORD PTR [esi+0x30]   // esi = esi[30]
d:  d9 e8                   fld1                              // 1
f:  dd 03                   fld    QWORD PTR [ebx]            // A; 1
11: d9 c0                   fld    st(0)                      // A; A; 1
13: d8 e2                   fsub   st,st(2)                   // A-1; A; 1
15: d9 e1                   fabs                              // |A-1|; A; 1
17: d8 c1                   fadd   st,st(1)                   // |A-1|+A; A; 1
19: d9 c9                   fxch   st(1)                      // A; |A-1|+A; 1
1b: d8 c2                   fadd   st,st(2)                   // A+1; |A-1|+A; 1
1d: d9 e1                   fabs                              // |A+1|; |A-1|+A; 1
1f: de e1                   fsubrp st(1),st                   // |A-1|+A-|A+1|; 1
21: dd 02                   fld    QWORD PTR [edx]            // B; |A-1|+A-|A+1|; 1
23: d9 c0                   fld    st(0)                      // B; B; |A-1|+A-|A+1|; 1
25: d8 e3                   fsub   st,st(3)                   // B-1; B; |A-1|+A-|A+1|; 1
27: d9 e1                   fabs                              // |B-1|; B; |A-1|+A-|A+1|; 1
29: d8 c1                   fadd   st,st(1)                   // |B-1|+B; B; |A-1|+A-|A+1|; 1
2b: d9 c9                   fxch   st(1)                      // B; |B-1|+B; |A-1|+A-|A+1|; 1
2d: d8 c3                   fadd   st,st(3)                   // B+1; |B-1|+B; |A-1|+A-|A+1|; 1
2f: d9 e1                   fabs                              // |B+1|; |B-1|+B; |A-1|+A-|A+1|; 1
31: de e1                   fsubrp st(1),st                   // |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
33: dd 01                   fld    QWORD PTR [ecx]            // C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
35: d9 c0                   fld    st(0)                      // C; C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
37: d8 e4                   fsub   st,st(4)                   // C-1; C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
39: d9 e1                   fabs                              // |C-1|; C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
3b: d8 c1                   fadd   st,st(1)                   // |C-1|+C; C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
3d: d9 c9                   fxch   st(1)                      // C; |C-1|+C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
3f: d8 c4                   fadd   st,st(4)                   // C+1; |C-1|+C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
41: d9 e1                   fabs                              // |C+1|; |C-1|+C; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
43: de e1                   fsubrp st(1),st                   // |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
45: d9 c0                   fld    st(0)                      // |C-1|+C-|C+1|; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
47: d8 c9                   fmul   st,st(1)                   // (|C-1|+C-|C+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
49: d9 c2                   fld    st(2)                      // |B-1|+B-|B+1|; (|C-1|+C-|C+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
4b: d8 cb                   fmul   st,st(3)                   // (|B-1|+B-|B+1|)^2; (|C-1|+C-|C+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
4d: de c1                   faddp  st(1),st                   // (|C-1|+C-|C+1|)^2+(|B-1|+B-|B+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
4f: d9 c3                   fld    st(3)                      // |A-1|+A-|A+1|; (|C-1|+C-|C+1|)^2+(|B-1|+B-|B+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
51: d8 cc                   fmul   st,st(4)                   // (|A-1|+A-|A+1|)^2; (|C-1|+C-|C+1|)^2+(|B-1|+B-|B+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
53: de c1                   faddp  st(1),st                   // (|C-1|+C-|C+1|)^2+(|B-1|+B-|B+1|)^2+(|A-1|+A-|A+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
55: dc 56 e0                fcom   QWORD PTR [esi-0x20]       // compare that sum with esi[-20]
58: df e0                   fnstsw ax                         // ax is result of comparison
5a: d0 ec                   shr    ah,1                       // but what is the actual comparison?
5c: 73 07                   jae    0x65                       // if >= go to 65
5e: dd d8                   fstp   st(0)                      // |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
60: dd 46 e8                fld    QWORD PTR [esi-0x18]       // D; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
63: eb 16                   jmp    0x7b                       // go to 7B

65: d9 e8                   fld1                              // 1; (|C-1|+C-|C+1|)^2+(|B-1|+B-|B+1|)^2+(|A-1|+A-|A+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
67: d8 d1                   fcom   st(1)                      // compare 1 with that sum
69: df e0                   fnstsw ax                         // ax is result of comparison
6b: d0 ec                   shr    ah,1                       // but what is the actual comparison?
6d: 72 07                   jb     0x76                       // if < go to 76
6f: dd d8                   fstp   st(0)                      // (|C-1|+C-|C+1|)^2+(|B-1|+B-|B+1|)^2+(|A-1|+A-|A+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
71: dc 7e f0                fdivr  QWORD PTR [esi-0x10]       // E / (|C-1|+C-|C+1|)^2+(|B-1|+B-|B+1|)^2+(|A-1|+A-|A+1|)^2; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
74: eb 05                   jmp    0x7b                       // go to 7B

76: de d9                   fcompp                            // redo that last comparison, |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
78: dd 46 f0                fld    QWORD PTR [esi-0x10]       // E; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1

7b: dd 41 08                fld    QWORD PTR [ecx+0x8]        // G; F = D or E or E / sum; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
7e: d8 c9                   fmul   st,st(1)                   // GF; F; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
80: dd 59 08                fstp   QWORD PTR [ecx+0x8]        // store GF where G was before, F; |C-1|+C-|C+1|; |B-1|+B-|B+1|; |A-1|+A-|A+1|; 1
83: dc cb                   fmul   st(3),st                   // F; |C-1|+C-|C+1|; |B-1|+B-|B+1|; F(|A-1|+A-|A+1|); 1
85: dc ca                   fmul   st(2),st                   // F; |C-1|+C-|C+1|; F(|B-1|+B-|B+1|); F(|A-1|+A-|A+1|); 1
87: de c9                   fmulp  st(1),st                   // F(|C-1|+C-|C+1|); F(|B-1|+B-|B+1|); F(|A-1|+A-|A+1|); 1
89: dd 19                   fstp   QWORD PTR [ecx]            // [ecx] = F(|C-1|+C-|C+1|), F(|B-1|+B-|B+1|); F(|A-1|+A-|A+1|); 1
8b: dd 1a                   fstp   QWORD PTR [edx]            // [edx] = F(|B-1|+B-|B+1|), F(|A-1|+A-|A+1|); 1
8d: dd 1b                   fstp   QWORD PTR [ebx]            // [ebx] = F(|A-1|+A-|A+1|), 1
8f: dd d8                   fstp   st(0)                      //
91: 89 d8                   mov    eax,ebx                    // restore eax
93: 5b                      pop    ebx                        // restore ebx
94: 5e                      pop    esi                        // restore esi
95: 5d                      pop    ebp                        // restore stack frame
96: c2 08 00                ret    0x8                        // that's it
```

This somehow describes a single iteration of something, and not yet a distance estimation. So, we have to figure out how it does distance estimation with these iterative functions.
