# DAY 15

Looking at the individual formulas separately, see how they interact. Koch Cube separately still doesn't look the same as MB3D, although the formula appears correct.

There is some magic going on in MB3Ds `RayMarch` procedure, so we might want to dive into that in more detail. They have a variable minimum distance somehow?

Ok, let's disect:

## `TRaymarchRec`

```pascal
TRaymarchRec = record
    PMCTparas: PMCTparameter;
    PIt3Dex: TPIteration3Dext;
    ActPos: TVec3D;
    MarchVec: TVec3D;  // direction of the march
    VievVec: TVec3D;
    ActZpos: Double;
    StartDEstop: Double;
    ZZposMul: Double;  // rays not exactly in view direction should take smaller steps?
    DEmulVary: Double;
    DEmulConst: Double;
    MaxRayLength: Double;
    BinSteps: Integer;
    seed: Integer;
    RMresult: Integer;  // 0: no object  1: object on DE  2: object on Itcount  3: Outside again?
    StepCount: Single;
    StepForward: Single;  // step multiplier/scale
    Zstepped: Single;  // on start: 0/1/2 = outside/insideConstStep/insideDIFSDE?
end;
```

## `TMCParameter`

```pascal
TMCTparameter = packed record    //MainCalcThread values
    iMinIt:           Integer;
    iMandWidth:       Integer;
    iThreadId:        Integer;
    FOVy:             Double;      //#12
    Xmit, Ymit, Zmit: Double;
    iSmNormals:       Word;       //#44
    IsCustomDE:       WordBool;
    mPsiLight:        TPsiLight5;
    msDEstop:         Single;
    sZstepDiv:        Single;
    iDEAddSteps:      Integer;
    pIt3Dext:         TPIteration3Dext;  //#64
    iMandHeight:      Integer;
    CalcDE:           TCaldDEfunction;
    PLVals:           TPLightVals;
    mctDEstopFactor:  Single;
    mctDEoffset:      Single;
    mctMH04ZSD:       Single;
    DEstop:           Single;
    sStepWm103:       Single;
    mZZ:              Double;
    mMandFunction:    TMandFunction;
    mMandFunctionDE:  TMandFunctionDE;
    FormulaType:      Integer;     //DEcombination mode  0: disable  1: min  2: max  3: avg   6:
    dDEscale2:        Single;
    Vgrads:           TMatrix3;
    StepWidth, Zend:  Double;      //Single?
    dCOX, dCOY, dCOZ: Double;
    dDEscale:         Single;
    dColPlus:         Single;
    Ystart:           TVec3D;  //->Campos
    HSvecs:           array[0..5] of TVec3D;  //put into HS calc threads?   VGradsFOV instead here?  + pointer to Iteration3Dext
    CAFX, CAFY:       Double;     //$1A0?
    PCalcThreadStats: TPCalcThreadStats;
    bVaryDEstop:      WordBool;
    NormalsOnDE:      WordBool;   //-> pointer to RMCalcNormals procedure
    pSiLight:         Pointer;    //pointer to begin of the SiLight array
    IsCustomDE1:      LongBool;
    IsCustomDE2:      LongBool;  //new for DEcomb hybrid
    ColorOption:      Byte;      //was: Integer;
    ColorOnIt:        Byte;
    DFogOnIt:         Word;
    sRoughness:       Single;    //$1CC   for normals calculation
    DEoption2:        Integer;   //$1D0
    mMandFunction2:   TMandFunction; //$1D4 new for DEcomb hybrid + IFS  , UP TO HERE DO NOT CHANGE OR ADD PARAMETER!!!!
    mMandFunctionDE2: TMandFunctionDE;
    MaxItsResult:     Integer;
    iMaxitF2:         Integer;
    mVgradsFOV:       TVec3D;    //$1e0  
    DEoptionResult:   Integer;
    iSliceCalc:       Integer;
    FOVXoff:          Single;    //for FOV calc, replaces 0.5 * iMandWidth in: CAFX := (0.5 * iMandWidth - ix) * FOVy / iMandHeight;
    FOVXmul:          Single;
    msDEsub:          Single; //= MinCS(0.9,  Sqrt(sZstepDiv));
    calcHardShadow:   Integer;     //was:#76   -> + option for 1HSsoft
    NaviStep, SLwidMNpix: Integer;   //for Navigator
    PLValsNavi:       TPLightValsNavi;
    iCutOptions:      Integer;
    MCTCameraOptic:   Integer;
    bMCTFirstStepRandom: LongBool;
    mctPlOpticZ:      Single;
    mctsM:            Single;
    mctColVarDEstopMul: Single;
    mctColorMul:      Single;
    iThreadCount:     Integer;
    FSIstart:         Integer;    //points to start of intern image cardinal array
    FSIoffset:        Integer;
    bMCTisValid:      LongBool;
    pCalcNormals:     Pointer;//TCalculateNormalsFunc;
    calc3D:           LongBool;
    sHSmaxLengthMultiplier: Single;
    SoftShadowRadius: Single;
    sZZstmitDif:      Single;
    DEAOmaxL:         Single;
    AOdither:         Integer;
    bCalcAmbShadow:   LongBool;
    CalcRect:         TRect;      //rect of calculation, the calculation includes the corners of the rect.
    SLoffset:         Integer;    //one row byte offset for SiLight array, when tiling it is smaller than width of image
    ZcMul:            Double;
    Zcorr:            Double;
    sDEcombSmooth:    Single;
    bInsideRendering: LongBool;
    bCalcInside:      LongBool;
    bInAndOutside:    LongBool;
    bIsIFS:           LongBool;   //for calcAmbshDE + inside
    DEmixCol:         Integer;
    FmixPow:          Single;
    RepeatFrom2:      Word;
    StartFrom2:       Word;
    iEnd2:            Integer;
    VLmul:            Single;     //for volumic light maps
    VLstepmul:        Single;
    Rstop3D:          Single;     // Sqr(It3Dex.RStop) * 64
    mctDEoffset006:   Single;
   // sZstepDiv2:       Single;     //test

    dJUw:             Double;
    dJUx, dJUy, dJUz: Double;     //+24    Julia start Values  copy from here to RepeatFrom from MCTparas = 168 byte
    pAl16vars:        Pointer;    //+48
    sPlaceholder:     Single;     //+52
    sPlaceholder2:    Double;     //+56
    iPlaceHolder:     Integer;    //+64
    iMaxIt:           Integer;    //+68
    dRStop:           Single;     //+72
    nHybrid:          array[0..5] of Integer;  //+76 Hybrid counts
    fHPVar:           array[0..5] of Pointer;  //+100 pointer to constants+vars, PVars-8=0.5, PVars->fHPVar[0]! dOptions below -8
    fHybrid:          array[0..5] of ThybridIteration2; //+124       fcustomIt -> fHybrid[0]!
    bCalcSIT:         ByteBool;   //+148   Bool + more options
    bFree:            Byte;       //+149
    wEndTo:           Word;       //+150
    bDoJulia:         LongBool;   //+152
    dLNRStop:         Single;     //+156
    DEoption:         Integer;    //+160    RepeatFrom2, EndTo
    fHln:             array[0..5] of Single;  //esi+164
    RepeatFrom1:      Word;       //+188
    StartFrom1:       Word;
    Smatrix4d:        TSmatrix4;
    pInitialization:  array[0..5] of TFormulaInitialization;
  //  DEoption2:        Integer;
 //   dWadd4dstep:      Double;

    withZBuffer:      boolean;
    MCrecordExt:      TPMCrecordExt;
    SLoffsetExt:      Integer;
end;
```

## `RayMarch`

```pascal
procedure RayMarch(RMrec: TPRaymarchRec);

var itmp: Integer;
    DElimited: LongBool;
    bFirstStep: LongBool;  // first step should be adjusted randomly
    RSFmul: Single;  // some multiplier...
    RLastStepWidth: Double;  // 
    dTmp: Double;  // most recently calculated distance estimation
    dT1: Double;
    RLastDE: Double;

begin

    with RMrec^ do
    begin

        PIt3Dex.CalcSIT := False;

        itmp := 0;
        StepCount := 0;
        Zstepped := 0;

        // initialize ZZposMul to how much MarchVec and ViewVec align
        ZZposMul := DotOfVectorsNormalize(@MarchVec , @VievVec);

        // initialize smallest distance msDEstop to StartDEstop (related to width of pixel)
        PMCTparas.msDEstop := StartDEstop;

        // initialize bFirstStep to whether or not the first step should be random, according to PMCTparas
        bFirstStep := RMrec.PMCTparas.bMCTFirstStepRandom;

        // initialize ActPos to c, ActPos is the current sample position
        mCopyVec(@ActPos, @PIt3Dex.C1);

        // make the first step
        Zstepped := Zstepped + StepForward;  // Zstepped is the current length of the ray
        mAddVecWeight(@PIt3Dex.C1, @MarchVec, StepForward);  // start with c a bit further out as well

        // do first distance estimation at c
        dTmp := PMCTparas.CalcDE(PIt3Dex, PMCTparas);

        // if that was a hit
        if (PIt3Dex.ItResultI >= PMCTparas.MaxItsResult) or (dTmp < PMCTparas.msDEstop) then
        begin

            // found by distance or by iteration
            if dTmp < PMCTparas.msDEstop then RMresult := 1 else RMresult := 2;

            // reset c to ActPos
            mCopyVec(@PIt3Dex.C1, @ActPos);

            Exit;
        end
        else
        begin

            // object was not yet found, march further

            // initialize the multiplier
            RSFmul := 1;

            // initialize RLastStepWidth (sZstepDiv comes from the header TMandHeader10, see elsewhere)
            RLastStepWidth := PMCTparas.sZstepDiv * dTmp;

            repeat

                // if already using too much iterations, do more accurate search towards the boundary
                if PIt3Dex.ItResultI >= PMCTparas.MaxItsResult then
                begin

                    // set dT1 to minus half of RLastStepWidth
                    dT1 := -0.5 * RLastStepWidth;

                    // add this to the total ray length
                    Zstepped := Zstepped + dT1;

                    // take half a step back in c as well, c -= dT1 * MarchVec
                    mAddVecWeight(@PIt3Dex.C1, @MarchVec, dT1);

                    // adjust msDEstop, the bailout distance, becomes slightly larger depending on the ray length and the view/march difference
                    PMCTparas.msDEstop := StartDEstop * (1 + Clamp0D(ActZpos + Zstepped * ZZposMul) * PMCTparas.mctDEstopFactor);

                    // and do distance estimation again
                    dTmp := PMCTparas.CalcDE(PIt3Dex, PMCTparas);

                    // RLastStepWidth *= 0.5
                    RLastStepWidth := -dT1;

                end;

                // if not enough iterations and still too far away
                if (PIt3Dex.ItResultI < PMCTparas.iMinIt) or
                    ((PIt3Dex.ItResultI < PMCTparas.MaxItsResult) and (dTmp >= PMCTparas.msDEstop)) then    //##### next step ######
                begin

                    // get closer

                    // initialize RLastDE to the most recent distance estimation
                    RLastDE := dTmp;

                    // scale distance by sZstepDiv * RSFmul for this step
                    dTmp := dTmp * PMCTparas.sZstepDiv * RSFmul;

                    // dT1 is msDEstop (>= 0.4) times some factor related to the pixel width
                    dT1 := MaxCS(PMCTparas.msDEstop, 0.4) * PMCTparas.mctMH04ZSD;

                    // if that is smaller than the most recent distance estimation (we would already be 'inside' the object)
                    if dT1 < dTmp then
                    begin

                        // make at most this step

                        // update StepCount accordingly
                        if not bFirstStep then StepCount := StepCount + dT1 / dTmp else StepCount := StepCount + Random;

                        // and set the new distance estimation to dT1
                        dTmp := dT1;
                    end
                    else
                    
                        // otherwise just add 1 to StepCount
                        StepCount := StepCount + 1;

                    // if first step start with random distance, apply this
                    if bFirstStep then   {..$Q-  integer overflow check}
                    begin

                        bFirstStep := False;
                        seed := 214013 * seed + 2531011;
                        dTmp := ((seed shr 16) and $7FFF) * 0.000030517578125 * dTmp;

                    end;

                    // and set RLastStepWidth to this new distance estimation
                    RLastStepWidth := dTmp;

                    // update ray length as well
                    Zstepped := Zstepped + dTmp;

                    // and move c in this direction
                    mAddVecWeight(@PIt3Dex.C1, @MarchVec, dTmp);

                    // also update msDEstop
                    PMCTparas.msDEstop := StartDEstop * (1 + Clamp0D(ActZpos + Zstepped * ZZposMul) * PMCTparas.mctDEstopFactor);

                    // do another new distance estimation        
                    dTmp := PMCTparas.CalcDE(PIt3Dex, PMCTparas);

                    // make sure the distance is getting smaller
                    if dTmp > RLastDE + RLastStepWidth then dTmp := RLastDE + RLastStepWidth;

                    // if the main step was bigger than this current estimation
                    if RLastDE > dTmp + s1em30 then
                    begin

                        // make dT1 equal to how much smaller that was with resp. to the new distance estimation
                        dT1 := RLastStepWidth / (RLastDE - dTmp);

                        // make sure it falls between 0.5 and 1
                        if dT1 < 1 then
                            RSFmul := maxCS(s05, dT1)
                        else
                            RSFmul := 1;
                    end

                    // otherwise make RSFmul just 1
                    else RSFmul := 1;
                end
                else     // ##### set found #####
                begin

                    // DElimited = true if there are still iterations left (so object was found on distance)
                    DElimited := PIt3Dex.ItResultI < PMCTparas.MaxItsResult;

                    // do additional steps if specified
                    if PMCTparas.iDEAddSteps > 0 then
                    begin

                        // do binary search during these additional steps?
                        if DElimited then
                            RMdoBinSearch(PMCTparas, dTmp, RLastStepWidth{, RLastDE})
                        else
                        begin
                            RMdoBinSearchIt(PMCTparas, Zstepped);
                        end;

                    end;

                    // update ActZpos
                    ActZpos := Clamp0D(ActZpos + Zstepped * ZZposMul);

                    // distinguish if object was found by distance or by iterations
                    if DElimited then RMresult := 1 else RMresult := 2;

                    // set c back to ActPos
                    mCopyVec(@PIt3Dex.C1, @ActPos);

                    Exit;

                end;

            // end when ray becomes too long or somebody canceled this thread
            until (Zstepped > MaxRayLength) or PMCTparas.PCalcThreadStats.pLBcalcStop^;  //or ActZpos' < 0? (frontclipping)

            // not found
            RMresult := 0;

            // but still update ActZpos
            ActZpos := Clamp0D(ActZpos + Zstepped * ZZposMul);
        end;
    end;
end;
```
